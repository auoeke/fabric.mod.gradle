plugins {
    id("fabric-loom").version("latest.integration")
    id("fabric-mod-gradle")
    id("groovy")
}

group = "net.auoeke"
version = "1.3.4"
description = "fabric-mod-gradle's test mod"

sourceSets {
    main {
        groovy.srcDir("src")
        resources.srcDir("resources")
    }

    empty {}
    comprehensive {}
}

dependencies {
    minecraft("com.mojang:minecraft:1.17.1")
    mappings("net.fabricmc:yarn:1.17.1+build.39:v2")
    modImplementation("net.fabricmc:fabric-loader:latest.integration")
}

mod {
    main {
        // Mod ID, version and description are inferred from the project.
        name = "test mod"

        author("auoeke") {
            email = "me@auoeke.net"
            irc = "todo"
            homepage = "https://auoeke.net"
            issues = "https://git.auoeke.net"
            sources = "https://git.auoeke.net"
        }

        license("GNU LGPL 3.0")

        entrypoints {
            main("net.auoeke.testmod.MainEntrypoint")
        }

        depends("fabric-language-groovy": "*")
    }

    empty {}

    comprehensive {
        id = "testmod"
        version = "1.3.4"
        name = "test mod"
        description = "fabric-mod-gradle's test mod: comprehensive demonstration"

        contact {
            email = "blockdestroyer68@example.net"
            irc = "irc://irc.example.net:6667/fabric-mod-gradle"
            homepage = "https://example.net"
            issues = "https://git.auoeke.net/fabric-mod-gradle"
            sources = "https://git.auoeke.net/fabric-mod-gradle/issues"
        }

        author("auoeke") {
            email = "me@auoeke.net"
            irc = "todo"
            homepage = "https://auoeke.net"
            issues = "https://git.auoeke.net"
            sources = "https://git.auoeke.net"
        }

        contributors {
            example {
                email = "example@example.net"
            }
            example2 {}
        }
        // or
        contributor("example") {
            email = "example@example.net"
        }
        contributor("example2")

        contributor("blockdestroyer68")

        licenses("GNU LGPL 3.0", "Apache 2.0")

        icon("assets/testmod/icon.png")
        // or
        // icon(64: "assets/testmod/icon.png")

        entrypoints {
            main("net.auoeke.testmod.MainEntrypoint")
            // or add("main", "net.auoeke.testmod.MainEntrypoint")

            client {
                value = "net.auoeke.testmod.GroovyEntrypoint"
                adapter = "groovy"
            }
            // or
            client("groovy", "net.auoeke.testmod.GroovyEntrypoint")
        }

        entrypoints.server("net.auoeke.testmod.ClientEntrypoint")

        languageAdapters {
            kotlin = "net.fabricmc.language.kotlin.KotlinAdapter"
        }

        languageAdapter("groovy", "net.fabricmc.language.groovy.GroovyLanguageAdapter")

        mixin("testmod.mixins.json")

        mixins {
            client("client.mixins.json")
            server("server.mixins.json")
        }
        // or
        mixin("client", "client.mixins.json")
        mixin("server", "server.mixins.json")

        depends(
            minecraft: ["1.16.5", "1.17", "1.17.1"],
            fabricloader: "*"
        )

        recommends(limitless: "1.5.0")
        suggests(sodium: "*")
        conflicts("hyphenated-mod-id": ["0.1.2", "1.2.3"])

        jars("jars/test0.jar", "jars/test1.jar")
    }
}
